set test "api-options"

set data_dir [exec bap --api-list-paths]

# mk tmp file and a link on it
set api_file [exec mktemp]
set api_name [exec basename $api_file]
exec ln -s $api_file
set link $api_name

# mk tmp path with a c:tmp_file
set api_path [exec mktemp -d]
exec mkdir -p $api_path/c
exec cp $api_file $api_path/c

#   test case name     command      path       expected                file
set cmds [subst {
    "api_show"           show       $api_path $api_name             ""
    "list-paths"         list-paths $api_path "$api_path"           ""
    "add api"            add        ""        "WARNING"             c:$api_file
    "add duplicate"      add        ""        "Will not overwrite"  c:$api_file
    "add not existed"    add        ""        "can't find api"      c:some_file
    "sanity check"       add        $api_path "Sanitization error"  c:$api_file
    "remove api"         rem        ""        "WARNING"             c:$api_file
    "remove api at path" rem        $api_path "WARNING"             c:$api_file
    "remove not existed" rem        ""        "Can't find api"      c:some_file
    "add api as a link"  add        ""        "WARNING"             c:$link
    "rem api as a link"  rem        ""        "WARNING"             c:$link
    "link as direcory"   add        $link     "not a directory"     c:$api_file
}]

foreach {name cmd path expected file} $cmds {
    if {$path == ""} {
        set full_cmd "--api-$cmd $file"
    } else {
        set full_cmd "--api-path $path --api-$cmd $file"
    }

    uplevel 0 spawn bap $full_cmd
    expect {
        $expected {pass "$test for $name"}
        default {fail "no diagnostic/wrong messages in $test for $name"}
    }
}


exec rm $api_file
exec rm -r $api_path
exec rm $api_name
